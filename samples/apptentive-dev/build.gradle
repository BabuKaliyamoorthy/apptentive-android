buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.3'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven { url 'http://oss.sonatype.org/content/repositories/snapshots/' }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 21

    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 22
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }

    productFlavors {
        dev {
            applicationId "com.apptentive.android.dev"
            manifestPlaceholders = [APPTENTIVE_API_KEY: APPTENTIVE_API_KEY_DEV,
                                    APPTENTIVE_USE_STAGING_SERVER: APPTENTIVE_USE_STAGING_SERVER_DEV,
                                    APPTENTIVE_APP_NAME: "Apptentive Dev2"];
        }
        qa {
            applicationId "com.apptentive.android.qa"
            manifestPlaceholders = [APPTENTIVE_API_KEY: APPTENTIVE_API_KEY_QA,
                                    APPTENTIVE_USE_STAGING_SERVER: APPTENTIVE_USE_STAGING_SERVER_QA,
                                    APPTENTIVE_APP_NAME: "Apptentive QA"];
        }
        rc {
            applicationId "com.apptentive.android.rc"
            manifestPlaceholders = [APPTENTIVE_API_KEY: APPTENTIVE_API_KEY_RC,
                                    APPTENTIVE_USE_STAGING_SERVER: APPTENTIVE_USE_STAGING_SERVER_RC,
                                    APPTENTIVE_APP_NAME: "Apptentive RC"];
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

dependencies {
    gradle.startParameter.taskRequests.each { taskRequest ->
        taskRequest.args.each { taskName ->
            String flavorName = "qa";
            if (taskName.toLowerCase().endsWith(flavorName+"debug") || taskName.toLowerCase().endsWith(flavorName+"release")) {
                qaCompile 'com.apptentive:apptentive-android:1.7.3-SNAPSHOT@aar'
            }
        }
    }
    compile 'net.hockeyapp.android:HockeySDK:3.5.0'
    devCompile project(':apptentive')
    rcCompile 'com.apptentive:apptentive-android:1.7.2@aar'
}

task run(type: Exec) {
    executable 'adb'
    args = ['shell', 'am', 'start', '-n', 'com.apptentive.android.dev/.MainActivity']
}
