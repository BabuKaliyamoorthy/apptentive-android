buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
        classpath 'com.google.gms:google-services:1.3.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

repositories {
    jcenter()
    maven { url 'http://oss.sonatype.org/content/repositories/snapshots/' }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 22

    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 22
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }

    productFlavors {
        dev {
            applicationId "com.apptentive.android.dev"
            manifestPlaceholders = [APPTENTIVE_API_KEY: APPTENTIVE_API_KEY_DEV,
                                    APPTENTIVE_USE_STAGING_SERVER: "false",
                                    APPTENTIVE_APP_NAME: "Apptentive Dev"];
        }
        qa {
            applicationId "com.apptentive.android.qa"
            manifestPlaceholders = [APPTENTIVE_API_KEY: APPTENTIVE_API_KEY_QA,
                                    APPTENTIVE_USE_STAGING_SERVER: "false",
                                    APPTENTIVE_APP_NAME: "Apptentive QA"];
        }
        rc {
            applicationId "com.apptentive.android.rc"
            manifestPlaceholders = [APPTENTIVE_API_KEY: APPTENTIVE_API_KEY_RC,
                                    APPTENTIVE_USE_STAGING_SERVER: "false",
                                    APPTENTIVE_APP_NAME: "Apptentive RC"];
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

dependencies {
    gradle.startParameter.taskRequests.each { taskRequest ->
        taskRequest.args.each { taskName ->
            String flavorName = "qa";
            if (taskName.toLowerCase().endsWith(flavorName+"debug") || taskName.toLowerCase().endsWith(flavorName+"release")) {
                qaCompile 'com.apptentive:apptentive-android:2.0.0-SNAPSHOT@aar'
            }
        }
    }
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.google.android.gms:play-services-gcm:7.8.0'
    compile 'net.hockeyapp.android:HockeySDK:3.5.0'
    devCompile project(':apptentive')
    rcCompile 'com.apptentive:apptentive-android:1.7.3@aar'
}

task run(type: Exec) {
    executable 'adb'
    args = ['shell', 'am', 'start', '-n', 'com.apptentive.android.dev/.MainActivity']
}
