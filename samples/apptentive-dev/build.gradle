buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven { url 'http://oss.sonatype.org/content/repositories/snapshots/' }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 15

    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 21
    }

    productFlavors {
        dev {
            applicationId "com.apptentive.android.dev"
            manifestPlaceholders = [APPTENTIVE_API_KEY: APPTENTIVE_API_KEY_DEV,
                                    APPTENTIVE_USE_STAGING_SERVER: APPTENTIVE_USE_STAGING_SERVER_DEV,
                                    APPTENTIVE_APP_NAME: "Apptentive Dev"];
        }
        qa {
            applicationId "com.apptentive.android.qa"
            manifestPlaceholders = [APPTENTIVE_API_KEY: APPTENTIVE_API_KEY_QA,
                                    APPTENTIVE_USE_STAGING_SERVER: APPTENTIVE_USE_STAGING_SERVER_QA,
                                    APPTENTIVE_APP_NAME: "Apptentive QA"];
        }
        rc {
            applicationId "com.apptentive.android.rc"
            manifestPlaceholders = [APPTENTIVE_API_KEY: APPTENTIVE_API_KEY_RC,
                                    APPTENTIVE_USE_STAGING_SERVER: APPTENTIVE_USE_STAGING_SERVER_RC,
                                    APPTENTIVE_APP_NAME: "Apptentive RC"];
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

dependencies {
    compile 'net.hockeyapp.android:HockeySDK:3.5.0'
    devCompile project(':apptentive')
    qaCompile 'com.apptentive:apptentive-android:1.7.2-SNAPSHOT@aar'
    //rcCompile(name:'apptentive-android-1.7.1', ext:'aar')
    rcCompile 'com.apptentive:apptentive-android:1.7.1@aar'
}

task run(type: Exec) {
    executable 'adb'
    args = ['shell', 'am', 'start', '-n', 'com.apptentive.android.dev/.MainActivity']
}
